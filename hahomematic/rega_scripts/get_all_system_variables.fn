!# get_all_system_variables.fn
!# Erstellt in Anlehnung an https://github.com/eq-3/occu/blob/45b38865f6b60f16f825b75f0bdc8a9738831ee0/WebUI/www/api/methods/sysvar/getall.tcl
!# Erweitert das Script um "description"
!#

var svList = dom.GetObject(ID_SYSTEM_VARIABLES);

string id;
boolean dpFirst = true;
string SYSVAR_EXT_MARKER = "hahm";
Write("[");
foreach(id, svList.EnumIDs())
{
    var sv = dom.GetObject(id);
    if (sv)
    {
      var sv_type = "UNKNOWN";
      if (sv.ValueSubType() == istAlarm   ) { sv_type = "ALARM";  }
      if (sv.ValueSubType() == istBool    ) { sv_type = "LOGIC";  }
      if (sv.ValueSubType() == istEnum    ) { sv_type = "LIST";   }
      if (sv.ValueSubType() == istGeneric ) { sv_type = "NUMBER"; }
      if (sv.ValueSubType() == istChar8859) { sv_type = "STRING"; }

      if (dpFirst) {
        dpFirst = false;
      } else {
        WriteLine(',');
      }
      Write("{");

      Write("\"id\": \"" # sv.ID() # "\",");
      Write("\"name\": \"" # sv.Name() # "\",");
      Write("\"type\": \"" # sv_type # "\",");
      Write("\"unit\": \"" # sv.ValueUnit() # "\",");
      Write("\"value\": \"" # sv.Value() # "\",");
      Write("\"hasExtMarker\": " # sv.DPInfo().ToLower().Contains(SYSVAR_EXT_MARKER) # ",");
      Write("\"isLogged\": " # sv.DPArchive() # ",");
      Write("\"isVisible\": " # sv.Visible() # ",");
      Write("\"isInternal\": " # sv.Internal() # ",");
      Write("\"channelId\": \"" # sv.Channel() # "\",");
      Write("\"minValue\": \"" # sv.ValueMin() # "\",");
      Write("\"maxValue\": \"" # sv.ValueMax() # "\",");
      Write("\"valueList\": \"" # sv.ValueList() # "\",");
      Write("\"valueName0\": \"" # sv.ValueName0() # "\",");
      Write("\"valueName1\": \"" # sv.ValueName1() # "\"");
      Write("}");
    }
}
Write("]");
